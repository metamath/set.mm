
  ${
    $d X x $. $d Y x $.
    lbslsat.v $e |- V = ( Base ` W ) $.
    lbslsat.n $e |- N = ( LSpan ` W ) $.
    lbslsat.z $e |- .0. = ( 0g ` W ) $.
    lbslsat.y $e |- Y = ( W |`s ( N ` { X } ) ) $.
    $( The basis of an atom.  (Contributed by Thierry Arnoux, 20-May-2023.) $)
    lbslsat $p |- ( ( W e. LVec /\ X e. V /\ X =/= .0. ) -> { X } e. ( LBasis `
       Y ) ) $=
      ( clvec wcel wne w3a csn cbs cfv wss clspn wceq vx cv cdif wn wral clbs
      wa clss simpl clmod lveclmod syl snssi adantl eqid lspcl syl2anc lsslvec
      3adant3 simpr lspssid snssg biimpar lspssv ressbas2 eleqtrd snssd lsslsp
      syl3anc eqtr3d c0g c0 difid fveq2i a1i eleq2d biimpd imp lsp0 adantr
      elsni cmnd cgrp lmodgrp grpmnd 3syl 0ellsp ress0g eqtr4d ex necon3ad
      3impia wb id sneq difeq2d fveq2d eleq12d notbid ralsng 3ad2ant2 mpbird
      islbs2 syl13anc ) CKLZDBLZDFMZNZEKLZDOZEPQZRZXJESQZQZXKTZUAUBZXJXPOZUCZXM
      QZLZUDZUAXJUEZXJEUFQZLZXEXFXIXGXEXFUGZXEXJAQZCUHQZLZXIXEXFUIYECUJLZXJBRZY
      HYEXEYIXEXFUICUKULXFYJXEDBUMUNYGXJABCGYGUOHUPUQYGYFCEJYGUOURUQUSXEXFXLXGY
      EDXKYEDYFXKYEXFXJYFRZDYFLZXEXFUTYEYIYJYKYEXEYIXEXFUICUKULXFYJXEDBUMUNXJAB
      CGHVAUQXFYLYKDYFBVBVCUQYEYFBRZYFXKTZYEYIYJYMYEXEYIXEXFUICUKULXFYJXEDBUMUN
      XJABCGHVDUQYFBECJGVEULVFVGUSXHYFXNXKXHYIYHYKYFXNTXEXFYIXGYEXEYIXEXFUICUKU
      LUSXEXFYHXGYEYIYJYHYEXEYIXEXFUICUKULXFYJXEDBUMUNYGXJABCGYGUOHUPUQUSXEXFYK
      XGYEYIYJYKYEXEYIXEXFUICUKULXFYJXEDBUMUNXJABCGHVAUQUSYFXJYGAXMCEJHXMUOYGUO
      VHVIXEXFYNXGYEYMYNYEYIYJYMYEXEYIXEXFUICUKULXFYJXEDBUMUNXJABCGHVDUQYFBECJG
      VEULUSVJXHYBDXJXJUCZXMQZLZUDZXEXFXGYRYEYQDFYEYQDFTYEYQUGZDEVKQZFYSDYTOZLD
      YTTYSDVLXMQZUUAYEYQDUUBLZYEYQUUCYEYPUUBDYPUUBTYEYOVLXMXJVMVNVOVPVQVRYEUUB
      UUATZYQYEEUJLZUUDYEXIUUEYEXEYHXIXEXFUIYEYIYJYHYEXEYIXEXFUICUKULXFYJXEDBUM
      UNYGXJABCGYGUOHUPUQYGYFCEJYGUOURUQEUKULXMEYTYTUOXMUOVSULVTVFDYTWAULYEFYTT
      ZYQYECWBLZFYFLZYMUUFYEYICWCLUUGYEXEYIXEXFUICUKULCWDCWEWFYEYIYJUUHYEXEYIXE
      XFUICUKULXFYJXEDBUMUNBXJACFIGHWGUQYEYIYJYMYEXEYIXEXFUICUKULXFYJXEDBUMUNXJ
      ABCGHVDUQYFBCEFJGIWHVIVTWIWJWKWLXFXEYBYRWMXGYAYRUADBXPDTZXTYQUUIXPDXSYPUU
      IWNUUIXRYOXMUUIXQXJXJXPDWOWPWQWRWSWTXAXBXIYDXLXOYBNUAXJYCXMXKEXKUOYCUOXMU
      OXCVCXD $.

    $( An atom has dimension 1.  (Contributed by Thierry Arnoux,
       20-May-2023.) $)
    lsatdim $p |- ( ( W e. LVec /\ X e. V /\ X =/= .0. ) -> ( dim ` Y ) = 1 )
       $=
      ( clvec wcel wne w3a cldim cfv csn chash c1 clbs wceq clss simp1 clmod
      wss lveclmod syl simp2 snssd eqid lspcl syl2anc lsslvec lbslsat dimval
      hashsng eqtrd ) CKLZDBLZDFMZNZEOPZDQZRPZSVAEKLZVCETPZLVBVDUAVAURVCAPZCUBP
      ZLZVEURUSUTUCVACUDLZVCBUEVIVAURVJURUSUTUCCUFUGVADBURUSUTUHUIVHVCABCGVHUJH
      UKULVHVGCEJVHUJUMULABCDEFGHIJUNVCEVFVFUJUOULVAUSVDSUAURUSUTUHDBUPUGUQ $.
  $}

  ${
    $d I j k $. $d R j k $. $d V j $.
    frlmdim.f $e |- F = ( R freeLMod I ) $.
    $( Dimension of a free left module.  (Contributed by Thierry Arnoux,
       20-May-2023.) $)
    frlmdim $p |- ( ( R e. DivRing /\ I e. V ) -> ( dim ` F ) = ( # ` I ) ) $=
      ( cdr wcel wa cldim cfv cuvc co crn chash clvec clbs wceq frlmlvec crg
      drngring adantr simpr eqid frlmlbs syl2anc dimval cbs wf1 cnzr drngnzr
      uvcf1 sylan hashf1rn wfn vj vk cv cur c0g cif cmpt cvv wral mptexg
      ad2antlr ralrimiva fnmpt syl uvcfval fneq1d mpbird hashfn 3eqtr2d ) AFGZC
      DGZHZBIJZACKLZMZNJZVRNJZCNJZVPBOGVSBPJZGZVQVTQABCDERVPASGZVOWDVNWEVOATUAV
      NVOUBAVRBCWCDEVRUCWCUCUDUEVSBWCWCUCUFUEVPVOCBUGJZVRUHZWAVTQVNVOUBVNAUIGVO
      WGAUJWFAVRCDBVRUCEWFUCUKULCWFVRDUMUEVPVRCUNZWAWBQVPWHUOCUPCUPUQZUOUQZQZAU
      RJZAUSJZUTZVAZVAZCUNZVPWOVBGZUOCVCWQVPWRUOCVOWRVNWJCGUPCWNDVDVEVFUOCWOWPV
      BWPUCVGVHVPCVRWPAVRWLUOUPCFDWMVRUCWLUCWMUCVIVJVKCVRVLVHVM $.
  $}

  ${
    $d A x y $. $d I x y $. $d R x y $.
    matdim.a $e |- A = ( I Mat R ) $.
    matdim.n $e |- N = ( # ` I ) $.
    $( Dimension of the space of square matrices. $)
    matdim $p |- ( ( I e. Fin /\ R e. DivRing ) -> ( dim ` A ) = ( N x. N ) )
       $=
      ( cfn wcel cdr wa cldim cfv chash cmul co cxp cfrlm wceq simpr simpl xpfi
      syl2anc eqid frlmdim vx vy cbs csca matbas eqcomd eqidd ssidd cv cplusg
      matplusg oveqdr cvsca clmod clvec frlmlvec lveclmod syl adantr simprl
      matsca fveq2d eleqtrd simprr lmodvscl syl3anc eleqtrrd matvsca crg
      drngring matlmod sylan2 matsca2 eqeltrrd islvec sylanbrc dimpropd hashxp
      3eqtr3d oveq12i syl6eqr ) CGHZBIHZJZAKLZCMLZWJNOZDDNOWHBCCPZQOZKLZWLMLZWI
      WKWHWGWLGHZWNWORWFWGSWHWFWFWPWFWGTWFWGTCCUAUBBWMWLGWMUCUDUBWHUEUFAUGLZAUH
      LZUGLZWMUHLZWRWMAWQWHWMUGLZWQABWMCIEWMUCUIUJWHWQUKWHWQULWHUEUMZWQHUFUMZWQ
      HZJUEUFWMUNLAUNLABWMCIEWMUCUOUPWHXBWSHZXDJZJZXBXCWMUQLZOZXAWQXGWMURHZXBWT
      UGLZHXCXAHXIXAHWHXJXFWHWMUSHZXJWHWGWPXLWFWGSWHWFWFWPWFWGTWFWGTCCUAUBBWMWL
      GWMUCUTUBWMVAVBVCXGXBWSXKWHXEXDVDWHWSXKRXFWHXKWSWHWTWRUGABWMCIEWMUCVEVFUJ
      VCVGXGXCWQXAWHXEXDVHWHWQXARZXFWHXAWQABWMCIEWMUCUIUJVCVGXBXHWTXKXAWMXCXAUC
      WTUCXHUCXKUCVIVJWHXMXFWHXAWQABWMCIEWMUCUIUJVCVKWHXFUEUFXHAUQLABWMCIEWMUCV
      LUPWTUCWRUCWHXKWSWHWTWRUGABWMCIEWMUCVEVFUJWHWSUKWHXEXCWSHJUEUFWTUNLWRUNLW
      HWTWRUNABWMCIEWMUCVEVFUPWHWGWPXLWFWGSWHWFWFWPWFWGTWFWGTCCUAUBBWMWLGWMUCUT
      UBWHAURHZWRIHAUSHWGWFBVMHXNBVNABCEVOVPWHBWRIABCIEVQWFWGSVRWRAWRUCVSVTWAWH
      WFWFWOWKRWFWGTWFWGTCCWBUBWCDWJDWJNFFWDWE $.
  $}
